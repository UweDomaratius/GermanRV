template t_berlint24(y) {
    //left_x, upper_y, width, height, offset_x, offset_y
	[  2, y,  6, 17,  -2, -10]
	[ 34, y, 18, 16, -10,  -6]
	[ 66, y, 25, 14, -12,  -9]
    [ 98, y, 18, 18,  -6,  -8]
    [130, y,  6, 21,  -2, -11]
    [162, y, 18, 17, -11,  -8]
    [194, y, 25, 14, -12,  -9]
    [226, y, 18, 16,  -5,  -6]
}

spriteset(berlint24_2cc, "graphics/berlint24.pcx") {
	t_berlint24(40)
}
spriteset(berlint24_2cc_loading, "graphics/berlint24.pcx") {
	t_berlint24(72)
}

spriteset(berlint24_falsecolor, "graphics/berlint24.pcx") {
	t_berlint24(104)
}

spriteset(berlint24_falsecolor_loading, "graphics/berlint24.pcx") {
	t_berlint24(136)
}

spritegroup berlint24_2cc_group {
	loading: berlint24_2cc_loading;
	loaded:  berlint24_2cc;
}

spritegroup berlint24_false_color_group {
	loading: berlint24_falsecolor_loading;
	loaded:  berlint24_falsecolor;
}

switch(FEAT_ROADVEHS, SELF, berlint24_graphics, cargo_subtype) {
	0: berlint24_2cc_group;
	berlint24_false_color_group;
}

item(FEAT_ROADVEHS, ID_22_BERLIN_T24) {
	property{
		name: string(TRAM_BERLIN_T24);
		introduction_date:date(1924,01,01);
		reliability_decay:21;
		vehicle_life:40;
        model_life:52;
        loading_speed:8;
        climates_available: bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPIC);
        speed:40 km/h;
        running_cost_factor:5;
        running_cost_base:RUNNING_COST_ROADVEH;
        sprite_id:SPRITE_ID_NEW_ROADVEH;
        cargo_capacity:59;
		default_cargo_type:PASS;
		cost_factor:13;
		length:8;
        power:92 hp;
        weight:13.25 tons;
        tractive_effort_coefficient:0.3;
        air_drag_coefficient: 0;
        retire_early:39;
        misc_flags: bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM);
		refittable_cargo_classes:bitmask(CC_PASSENGERS);
	}
	graphics {
		default: berlint24_graphics;
		purchase: berlint24_2cc;
		colour_mapping: tram_cb_colour_mapping_historic_cities;
		cargo_subtype_text: tram_cb_refit_historic_cities;
	}
}
template t_gtw_21(y) {
    //left_x, upper_y, width, height, offset_x, offset_y
	[  2, y,  8, 17,  -4,  -8]
	[ 34, y, 19, 16,  -9,  -8]
	[ 66, y, 19, 17,  -9, -13]
    [ 98, y, 19, 16, -10,  -9]
    [130, y,  8, 17,  -4,  -8]
    [162, y, 19, 16, -15,  -7]
    [194, y, 19, 17,  -9, -11]
    [226, y, 19, 16,  -7,  -9]
}

template t_gtw_35(y) {
    //left_x, upper_y, width, height, offset_x, offset_y
	[  2, y,  8, 15,  -4,  -8]
	[ 34, y, 16, 16,  -8,  -7]
	[ 66, y, 15, 17,  -7, -12]
    [ 98, y, 16, 16, -10,  -9]
    [130, y,  8, 15,  -4,  -8]
    [162, y, 16, 16, -13,  -7]
    [194, y, 15, 17,  -7, -11]
    [226, y, 16, 16,  -7, -11]
}

template t_gtw_wagon(y) {
    //left_x, upper_y, width, height, offset_x, offset_y
	[  2, y,  8, 18,  -4, -9]
	[ 34, y, 17, 15,  -9, -6]
	[ 66, y, 20, 12, -10, -6]
    [ 98, y, 17, 15, -10, -9]
    [130, y,  8, 18,  -4, -9]
    [162, y, 17, 15, -15, -6]
    [194, y, 20, 12, -10, -6]
    [226, y, 17, 15,  -6, -8]
}

spriteset(gtw21, "graphics/gtw21_35.pcx") {
	t_gtw_21(40)
}
spriteset(gtw35, "graphics/gtw21_35.pcx") {
	t_gtw_35(72)
}

spriteset(gtw_wagon_open_empty, "graphics/gtw21_35.pcx") {
	t_gtw_wagon(104)
}
spriteset(gtw_wagon_open_half_full_canisters, "graphics/gtw21_35.pcx") {
	t_gtw_wagon(136)
}

spriteset(gtw_wagon_open_full_canisters, "graphics/gtw21_35.pcx") {
	t_gtw_wagon(168)
}

spriteset(gtw_wagon_open_half_full_crates, "graphics/gtw21_35.pcx") {
	t_gtw_wagon(200)
}

spriteset(gtw_wagon_open_full_crates, "graphics/gtw21_35.pcx") {
	t_gtw_wagon(232)
}

spriteset(gtw_wagon_open_tarpaulin, "graphics/gtw21_35.pcx") {
	t_gtw_wagon(264)
}

spriteset(gtw_wagon_open_half_full_bulk, "graphics/gtw21_35.pcx") {
	t_gtw_wagon(296)
}

spriteset(gtw_wagon_open_full_bulk, "graphics/gtw21_35.pcx") {
	t_gtw_wagon(328)
}

spriteset(gtw_wagon_stakes_empty, "graphics/gtw21_35.pcx") {
	t_gtw_wagon(360)
}

spriteset(gtw_wagon_stakes_steel, "graphics/gtw21_35.pcx") {
	t_gtw_wagon(392)
}

spriteset(gtw_wagon_stakes_paper, "graphics/gtw21_35.pcx") {
	t_gtw_wagon(424)
}

spriteset(gtw_wagon_stakes_paper_tarpaulin, "graphics/gtw21_35.pcx") {
	t_gtw_wagon(456)
}

spriteset(gtw_wagon_stakes_wood, "graphics/gtw21_35.pcx") {
	t_gtw_wagon(488)
}

spriteset(gtw_wagon_stakes_cabledrum, "graphics/gtw21_35.pcx") {
	t_gtw_wagon(520)
}

spriteset(gtw_wagon_livestock, "graphics/gtw21_35.pcx") {
	t_gtw_wagon(552)
}

spriteset(gtw_wagon_livestock_loading, "graphics/gtw21_35.pcx") {
	t_gtw_wagon(584)
}

spriteset(gtw_wagon_closed, "graphics/gtw21_35.pcx") {
	t_gtw_wagon(616)
}

spriteset(gtw_wagon_closed_loading_empty, "graphics/gtw21_35.pcx") {
	t_gtw_wagon(648)
}

spriteset(gtw_wagon_closed_loading_half, "graphics/gtw21_35.pcx") {
	t_gtw_wagon(680)
}

spriteset(gtw_wagon_closed_loading_full, "graphics/gtw21_35.pcx") {
	t_gtw_wagon(712)
}

spritegroup gtw_wagon_open_canisters_group {
	loading: [gtw_wagon_open_empty, gtw_wagon_open_half_full_canisters, gtw_wagon_open_full_canisters];
	loaded:  [gtw_wagon_open_empty, gtw_wagon_open_half_full_canisters, gtw_wagon_open_full_canisters];
}

spritegroup gtw_wagon_open_crates_group {
	loading: [gtw_wagon_open_empty, gtw_wagon_open_half_full_crates, gtw_wagon_open_full_crates];
	loaded:  [gtw_wagon_open_empty, gtw_wagon_open_half_full_crates, gtw_wagon_open_full_crates];
}

spritegroup gtw_wagon_open_bulk_covered_group {
	loading: [gtw_wagon_open_empty, gtw_wagon_open_half_full_bulk, gtw_wagon_open_full_bulk];
	loaded:  [gtw_wagon_open_empty, gtw_wagon_open_tarpaulin];
}

spritegroup gtw_wagon_open_bulk_group {
	loading: [gtw_wagon_open_empty, gtw_wagon_open_half_full_bulk, gtw_wagon_open_full_bulk];
	loaded:  [gtw_wagon_open_empty, gtw_wagon_open_half_full_bulk, gtw_wagon_open_full_bulk];
}

spritegroup gtw_wagon_stakes_wood_group {
	loading: [gtw_wagon_stakes_empty, gtw_wagon_stakes_wood];
	loaded:  [gtw_wagon_stakes_empty, gtw_wagon_stakes_wood];
}

spritegroup gtw_wagon_stakes_paper_group {
	loading: [gtw_wagon_stakes_empty, gtw_wagon_stakes_paper];
	loaded:  [gtw_wagon_stakes_empty, gtw_wagon_stakes_paper_tarpaulin];
}

spritegroup gtw_wagon_stakes_steel_group {
	loading: [gtw_wagon_stakes_empty, gtw_wagon_stakes_steel];
	loaded:  [gtw_wagon_stakes_empty, gtw_wagon_stakes_steel];
}

spritegroup gtw_wagon_stakes_cabledrum_group {
	loading: [gtw_wagon_stakes_empty, gtw_wagon_stakes_cabledrum];
	loaded:  [gtw_wagon_stakes_empty, gtw_wagon_stakes_cabledrum];
}

spritegroup gtw_wagon_livestock_group {
	loading: gtw_wagon_livestock_loading;
	loaded:  gtw_wagon_livestock;
}

spritegroup gtw_wagon_closed_group {
	loading: [gtw_wagon_closed_loading_empty, gtw_wagon_closed_loading_half, gtw_wagon_closed_loading_full];
	loaded:  gtw_wagon_closed;
}

spriteset(gtw21_menu, "graphics/gtw21_35.pcx") {
	[  194, 744,  45,17,  -22, -9]
}

spriteset(gtw35_menu, "graphics/gtw21_35.pcx") {
	[  2, 808,  45,17,  -22, -9]
}

switch(FEAT_ROADVEHS, SELF, gtw21_graphics_bulk, (cargo_classes & bitmask(CC_COVERED)) == bitmask(CC_COVERED)) {
	CC_COVERED: gtw_wagon_open_bulk_covered_group;
	gtw_wagon_open_bulk_group;
}

switch(FEAT_ROADVEHS, SELF, gtw21_graphics_is_bulk, ((cargo_classes & bitmask(CC_NON_POURABLE)) == 0)&&(cargo_classes & bitmask(CC_BULK)) == bitmask(CC_BULK)) {
	1: gtw21_graphics_bulk;
	gtw_wagon_closed_group;
}

switch(FEAT_ROADVEHS, SELF, gtw21_graphics_is_liquid, (cargo_classes & bitmask(CC_LIQUID)) == bitmask(CC_LIQUID)) {
	1: gtw_wagon_open_canisters_group;
	gtw21_graphics_is_bulk; // default, anything that has not been mentioned yet
}

switch(FEAT_ROADVEHS, SELF, gtw21_graphics_wagons, cargo_type_in_veh) {
	//List cargos here that require specific handling
	LVST: gtw_wagon_livestock_group;
	STEL: gtw_wagon_stakes_steel_group;
	PAPR: gtw_wagon_stakes_paper_group;
	STAL: gtw_wagon_stakes_cabledrum_group;
	WOOD: gtw_wagon_stakes_wood_group;
	WDPR: gtw_wagon_stakes_wood_group;
	GOOD: gtw_wagon_open_crates_group;
	gtw21_graphics_is_liquid; // other cargos are treated via their cargo classes
}

switch(FEAT_ROADVEHS, SELF, gtw21_graphics, position_in_consist) {
	0: gtw21;
	gtw21_graphics_wagons;
}

switch(FEAT_ROADVEHS, SELF, gtw21_cb_colour_mapping, position_in_consist > 0 && (cargo_classes & bitmask(CC_BULK)) == bitmask(CC_BULK)) {
	1: cb_colour_bulk_freight;
	return CB_FAILED;
}

switch(FEAT_ROADVEHS, SELF, gtw21_cb_articulation, extra_callback_info1) {
	1..3: return ID_30_GTw21;
	return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}


switch(FEAT_ROADVEHS, SELF, gtw21_cb_cargo_capacity, 
	[STORE_TEMP(param_cargo_capacity == 0 ? 100 : param_cargo_capacity, 0),
	position_in_consist]) {
	0: return 0;
	return (8 * 16 * LOAD_TEMP(0)) / (cargo_unit_weight*100);
}

item(FEAT_ROADVEHS, ID_30_GTw21) {
	property{
		name: string(TRAM_GTW21);
		introduction_date:date(1920,01,01);
		reliability_decay:21;
		vehicle_life:25;
        model_life:40;
        loading_speed:4;
        climates_available: bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPIC);
        speed:35 km/h;
        running_cost_factor:17;
        running_cost_base:RUNNING_COST_ROADVEH;
        sprite_id:SPRITE_ID_NEW_ROADVEH;
        cargo_capacity:6;
		default_cargo_type:COAL;
		cost_factor:25;
		sound_effect:sound("src/bell1.wav");
		length:7;
        power:110 hp;
        weight:15 tons;
        tractive_effort_coefficient:0.3;
        air_drag_coefficient: 0;
        retire_early:17;
        misc_flags: bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM);
		refittable_cargo_classes:ALL_NORMAL_CARGO_CLASSES;
		non_refittable_cargo_classes:bitmask(CC_PASSENGERS);
	}
	graphics {
		default: gtw21_graphics;
		purchase: gtw21_menu;
		sound_effect:tram_cb_sound;
		colour_mapping:gtw21_cb_colour_mapping;
		cargo_capacity: gtw21_cb_cargo_capacity;
		purchase_cargo_capacity: return 6 * ((param_cargo_capacity == 0) ? 100 : param_cargo_capacity)/100;
		articulated_part:gtw21_cb_articulation;
	}
}

switch(FEAT_ROADVEHS, SELF, gtw35_graphics, position_in_consist) {
	0: gtw35;
	gtw21_graphics_wagons; // shared wagons for GTw21 and GTw35
}

switch(FEAT_ROADVEHS, SELF, gtw35_cb_cargo_capacity, 
	[STORE_TEMP(param_cargo_capacity == 0 ? 100 : param_cargo_capacity, 0),
	position_in_consist]) {
	0: return 0;
	return (10 * 16 * LOAD_TEMP(0)) / (cargo_unit_weight*100);
}

switch(FEAT_ROADVEHS, SELF, gtw35_cb_articulation, extra_callback_info1) {
	1..4: return ID_31_GTw35;
	return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_ROADVEHS, ID_31_GTw35) {
	property{
		name: string(TRAM_GTW35);
		introduction_date:date(1935,01,01);
		reliability_decay:16;
		vehicle_life:25;
        model_life:45;
        loading_speed:7;
        climates_available: bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPIC);
        speed:40 km/h;
        running_cost_factor:46;
        running_cost_base:RUNNING_COST_ROADVEH;
        sprite_id:SPRITE_ID_NEW_ROADVEH;
        cargo_capacity:8;
		default_cargo_type:COAL;
		cost_factor:66;
		sound_effect:sound("src/bell1.wav");
		length:7;
        power:170 hp;
        weight:20 tons;
        tractive_effort_coefficient:0.3;
        air_drag_coefficient: 0;
        retire_early:17;
        misc_flags: bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM);
		refittable_cargo_classes:ALL_NORMAL_CARGO_CLASSES;
		non_refittable_cargo_classes:bitmask(CC_PASSENGERS);
	}
	graphics {
		default: gtw35_graphics;
		purchase: gtw35_menu;
		sound_effect:tram_cb_sound;
		colour_mapping:gtw21_cb_colour_mapping; // logic is shared between GTw21 and GTw35
		purchase_cargo_capacity: return 8 * ((param_cargo_capacity == 0) ? 100 : param_cargo_capacity)/100;
		cargo_capacity:gtw35_cb_cargo_capacity;
		articulated_part:gtw35_cb_articulation;
	}
}
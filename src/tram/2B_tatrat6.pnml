template t_tatrat6_front(y) {
    //left_x, upper_y, width, height, offset_x, offset_y
	[  2, y,  6, 24,  -3, -12]
	[ 66, y, 22, 20,  -9, -11]
	[130, y, 34, 16, -17, -12]
    [194, y, 22, 18, -11,  -9]
    [258, y,  6, 24,  -3, -12]
    [322, y, 22, 18, -13,  -9]
    [386, y, 34, 16, -20, -11]
    [450, y, 22, 20,  -8, -12]
}

template t_tatrat6_back(y) {
    //left_x, upper_y, width, height, offset_x, offset_y
	[  2, y,  6, 24,  -3, -12]
	[ 66, y, 22, 18,  -9, -10]
	[130, y, 34, 12, -17, -12]
    [194, y, 22, 18, -11,  -9]
    [258, y,  6, 24,  -3, -12]
    [322, y, 22, 18, -13,  -9]
    [386, y, 34, 12, -20, -11]
    [450, y, 22, 18,  -8, -11]
}

spriteset(tatrat6_2cc_front, "graphics/tatrat6.pcx") {
	t_tatrat6_front(72)
}
spriteset(tatrat6_2cc_front_loading, "graphics/tatrat6.pcx") {
	t_tatrat6_front(136)
}

spriteset(tatrat6_2cc_front_modern, "graphics/tatrat6.pcx") {
	t_tatrat6_front(200)
}
spriteset(tatrat6_2cc_front_modern_loading, "graphics/tatrat6.pcx") {
	t_tatrat6_front(264)
}

spriteset(tatrat6_2cc_back, "graphics/tatrat6.pcx") {
	t_tatrat6_back(328)
}

spriteset(tatrat6_2cc_back_loading, "graphics/tatrat6.pcx") {
	t_tatrat6_back(392)
}

spriteset(tatrat6_false_color_front, "graphics/tatrat6.pcx") {
	t_tatrat6_front(456)
}

spriteset(tatrat6_false_color_front_loading, "graphics/tatrat6.pcx") {
	t_tatrat6_front(520)
}

spriteset(tatrat6_false_color_front_modern, "graphics/tatrat6.pcx") {
	t_tatrat6_front(584)
}

spriteset(tatrat6_false_color_front_modern_loading, "graphics/tatrat6.pcx") {
	t_tatrat6_front(648)
}

spriteset(tatrat6_false_color_back, "graphics/tatrat6.pcx") {
	t_tatrat6_back(712)
}

spriteset(tatrat6_false_color_back_loading, "graphics/tatrat6.pcx") {
	t_tatrat6_back(776)
}

spritegroup tatrat6_2cc_front_group {
	loading: tatrat6_2cc_front_loading;
	loaded:  tatrat6_2cc_front;
}

spritegroup tatrat6_2cc_front_modern_group {
	loading: tatrat6_2cc_front_modern_loading;
	loaded:  tatrat6_2cc_front_modern;
}

spritegroup tatrat6_2cc_back_group {
	loading: tatrat6_2cc_back_loading;
	loaded:  tatrat6_2cc_back;
}

spritegroup tatrat6_false_color_front_group {
	loading: tatrat6_false_color_front_loading;
	loaded:  tatrat6_false_color_front;
}

spritegroup tatrat6_false_color_front_modern_group {
	loading: tatrat6_false_color_front_modern_loading;
	loaded:  tatrat6_false_color_front_modern;
}

spritegroup tatrat6_false_color_back_group {
	loading: tatrat6_false_color_back_loading;
	loaded:  tatrat6_false_color_back;
}

spriteset(tatrat6_menu, "graphics/tatrat6.pcx") {
	[  386, 840,  51,16,  -25, -9]
}

switch(FEAT_ROADVEHS, SELF, tatrat6_cb_colour_mapping, date_of_last_service < date(1991,1,1)) {
	1: tram_cb_colour_mapping_east_cities_old;
	tram_cb_colour_mapping_east_cities_modern;
}

switch(FEAT_ROADVEHS, SELF, tatrat6_graphics_2cc, position_in_consist) {
	0: tatrat6_2cc_front_group;
	tatrat6_2cc_back_group;
}

switch(FEAT_ROADVEHS, SELF, tatrat6_graphics_false_color, position_in_consist) {
	0: tatrat6_false_color_front_group;
	tatrat6_false_color_back_group;
}

switch(FEAT_ROADVEHS, SELF, tatrat6_graphics_2cc_modern, position_in_consist) {
	0: tatrat6_2cc_front_modern_group;
	tatrat6_2cc_back_group;
}

switch(FEAT_ROADVEHS, SELF, tatrat6_graphics_false_color_modern, position_in_consist) {
	0: tatrat6_false_color_front_modern_group;
	tatrat6_false_color_back_group;
}

switch(FEAT_ROADVEHS, SELF, tatrat6_graphics_old, cargo_subtype) {
	0: tatrat6_graphics_2cc;
	tatrat6_graphics_false_color;
}

switch(FEAT_ROADVEHS, SELF, tatrat6_graphics_modern, cargo_subtype) {
	0: tatrat6_graphics_2cc_modern;
	tatrat6_graphics_false_color_modern;
}

switch(FEAT_ROADVEHS, SELF, tatrat6_graphics, date_of_last_service < date(1991,1,1)) {
	1: tatrat6_graphics_old;
	tatrat6_graphics_modern;
}

switch(FEAT_ROADVEHS, SELF, tatrat6_cb_articulation, extra_callback_info1) {
	1: return ID_2B_TATRA_T6A2;
	return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch(FEAT_ROADVEHS, SELF, tatrat6_cb_refitted_capacity, 
	[STORE_TEMP(param_passenger_capacity == 0 ? 100 : param_passenger_capacity, 0)]) {
	return 115 * LOAD_TEMP(0) / 100;
}

item(FEAT_ROADVEHS, ID_2B_TATRA_T6A2) {
	property{
		name: string(TRAM_TATRA_T6A2);
		introduction_date:date(1988,01,01);
		reliability_decay:19;
		vehicle_life:30;
        model_life:42;
        loading_speed:23;
        climates_available: bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPIC);
        speed:55 km/h;
        running_cost_factor:106;
        running_cost_base:RUNNING_COST_ROADVEH;
        sprite_id:SPRITE_ID_NEW_ROADVEH;
        cargo_capacity:115; // half of the capacity since it is articulated
		default_cargo_type:PASS;
		cost_factor:183;
		sound_effect:sound("src/bell1.wav");
		length:8;
        power:245 hp;
        weight:16.25 tons;
        tractive_effort_coefficient:0.3;
        air_drag_coefficient: 0;
        retire_early:31;
        misc_flags: bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM);
		refittable_cargo_classes:bitmask(CC_PASSENGERS);
	}
	graphics {
		default: tatrat6_graphics;
		purchase: tatrat6_menu;
		articulated_part:tatrat6_cb_articulation;
		colour_mapping: tatrat6_cb_colour_mapping;
		cargo_subtype_text: tram_cb_refit_east_cities;
		cargo_capacity: tatrat6_cb_refitted_capacity;
	}
}